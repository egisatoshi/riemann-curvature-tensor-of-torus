;;; Coordinates for Torus

(define $x [|θ φ|])

(define $X [|(* '(+ (* a (cos θ)) b) (cos φ)) ; = x
             (* '(+ (* a (cos θ)) b) (sin φ)) ; = y
             (* a (sin θ))                    ; = z
             |])

;; Local coordinates

(define $e ((∂/∂ X_# $) x~#))

;; Metric tensor

(define $g__ (generate-tensor 2#(V.* e_%1 e_%2) {2 2}))
(define $g~~ (M.inverse g_#_#))

;; Christoffel symbols of the first kind

(define $Γ___
  (with-symbols {j k l}
    (* (/ 1 2)
       (+ (∂/∂ g_j_l x_k)
          (∂/∂ g_j_k x_l)
          (* -1 (∂/∂ g_k_l x_j))))))

;; Christoffel symbols of the second kind

(define $Γ~__
  (with-symbols {i j k l}
    (. g~i~j Γ_j_k_l)))

;; Riemann curvature tensor

(define $R~___
  (with-symbols {i j k l m}
    (+ (- (∂/∂ Γ~i_j_k x_l) (∂/∂ Γ~i_j_l x_k))
       (- (. Γ~m_j_k Γ~i_m_l) (. Γ~m_j_l Γ~i_m_k)))))

(define $R____ (with-symbols {i} (. g_i_# R~i_#_#_#)))

;; Ricci curvature

(define $Ric__ (with-symbols {i j k} (contract + R~i_j_k_i)))

;; Scalar curvature

(define $scalar-curvature (with-symbols {j k} (. g~j~k Ric_j_k)))

;; Covariant derivative of Riemann curvature tensor

(define $∇R_____
  (with-symbols {i j k l m n}
    (- (∂/∂ R_i_j_k_l x_m)
       (. Γ~n_m_i R_n_j_k_l)
       (. Γ~n_m_j R_i_n_k_l)
       (. Γ~n_m_k R_i_j_n_l)
       (. Γ~n_m_l R_i_j_k_n))))

;; Second Bianchi identity

(with-symbols {i j k l m} (+ ∇R_i_j_k_l_m ∇R_i_j_l_m_k ∇R_i_j_m_k_l))
